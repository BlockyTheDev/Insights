plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

String packagePath = 'net.frankheijden.insights'
group = packagePath
version = '5.5.4'
description = 'Insights'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://maven.sk89q.com/repo/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.maven.apache.org/maven2' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
}

dependencies {
    implementation 'io.papermc:paperlib:1.0.2'
    implementation 'org.bstats:bstats-bukkit:1.7'
    implementation 'com.github.InsightsPlugin:WECompatibility:v1.8.0'
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.4'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.1.0'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.2'

    testCompile 'org.assertj:assertj-core:3.15.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

artifacts {
    archives shadowJar
}

shadowJar {
    relocate('org.bstats.bukkit', packagePath + '.dependencies.bstats')
    relocate('io.papermc.lib', packagePath + '.dependencies.paperlib')
    relocate('net.frankheijden.wecompatibility', packagePath + '.dependencies.wecompatibility')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    from('src/main/resources') {
        include 'plugin.yml'
        expand(version: project.version)
    }
}

test {
    useJUnitPlatform()
}
