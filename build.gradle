plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

String packagePath = 'net.frankheijden.insights'
group = packagePath
version = '4.1.3'
description = 'Insights'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.frankheijden.net/repository/minecraft/' }
    maven { url = 'https://maven.sk89q.com/repo/' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'http://repo.maven.apache.org/maven2' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
}

dependencies {
    implementation 'io.papermc:paperlib:1.0.2'
    implementation 'org.bstats:bstats-bukkit:1.7'
    compileOnly 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.4'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.1.0'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.2'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

artifacts {
    archives shadowJar
}

shadowJar {
    relocate('org.bstats.bukkit', packagePath + '.dependencies.bstats')
    relocate('io.papermc.lib', packagePath + '.dependencies.paperlib')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    from('src/main/resources') {
        include 'plugin.yml'
        expand(version: project.version)
    }
}

test {
    useJUnitPlatform()
}
